//--------------------------------------------------------------------------------------------------
// Kibosh Gradle build file
//--------------------------------------------------------------------------------------------------

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("eu.kakde.gradle.sonatype-maven-central-publisher") version "1.0.6"
}

def GROUP_ID = "org.dx42"
def ARTIFACT_ID = "Kibosh"
def VERSION = "1.1.1"
def LICENSE_NAME = 'The Apache License, Version 2.0'
def LICENSE_TEXT_URL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
def GIT_SCM_URL = 'scm:git:git@github.com:dx42/Kibosh.git'
def GITHUB_REPOSITORY_URL = 'https://github.com/dx42/Kibosh'

version = VERSION
group = GROUP_ID
archivesBaseName = ARTIFACT_ID

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

def junitVersion = '5.10.2'
def slf4jVersion = '1.6.6'
def lombokVersion = '1.18.30'

dependencies {
    // These dependencies are exported to consumers
    api "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation "org.slf4j:slf4j-simple:${slf4jVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    testImplementation 'org.assertj:assertj-core:3.25.1'
    testImplementation "org.mockito:mockito-core:2.28.2"
}

test {
    useJUnitPlatform()
}

//------------------------------------------------------------------------------
// Publish to Maven Central
//------------------------------------------------------------------------------
sonatypeCentralPublishExtension {
    // Set group ID, artifact ID, version, and other publication details
    groupId.set(GROUP_ID)
    artifactId.set(ARTIFACT_ID)
    version.set(VERSION)
    componentType.set("java") // "java" or "versionCatalog"
    publishingType.set("AUTOMATIC") // USER_MANAGED or AUTOMATIC

    // Set username and password for Sonatype repository
    username.set(sonatypeUsername)
    password.set(sonatypePassword)

    // Configure POM metadata
    pom {
        name.set("Kibosh")
        description.set("The Kibosh project provides simple static analysis for Java source files designed to be run as part of a test suite.")
        url.set(GITHUB_REPOSITORY_URL)
        licenses {
            license {
                name.set(LICENSE_NAME)
                url.set(LICENSE_TEXT_URL)
            }
        }
        developers {
            developer {
                id.set("chrismair")
                name.set("Chris Mair")
                organization.set("dx42")
                organizationUrl.set("dx42.org")
            }
        }
        scm {
            url.set(GITHUB_REPOSITORY_URL)
            connection.set(GIT_SCM_URL)
            developerConnection.set(GIT_SCM_URL)
        }
        issueManagement {
            system.set("GitHub")
            url.set(GITHUB_REPOSITORY_URL + "/issues")
        }
    }
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Kibosh'
                description = 'The Kibosh project provides simple static analysis for Java source files designed to be run as part of a test suite.'
                url = GITHUB_REPOSITORY_URL

                scm {
                    connection = GIT_SCM_URL
                    developerConnection = GIT_SCM_URL
                    url = GIT_SCM_URL
                }

                licenses {
                    license {
                        name = LICENSE_NAME
                        url = LICENSE_TEXT_URL
                    }
                }

                developers {
                    developer {
                        id = 'chrismair'
                        name = 'Chris Mair'
                        email = 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }

    println "sonatypeUsername=" + sonatypeUsername
    println "sonatypePassword=" + sonatypePassword

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

}
