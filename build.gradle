//--------------------------------------------------------------------------------------------------
// Kibosh Gradle build file
//--------------------------------------------------------------------------------------------------

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

version = "0.1.0"
group = 'org.dx42'
archivesBaseName = 'Kibosh'

repositories {
    mavenCentral()
}

def junitVersion = '5.10.2'
def slf4jVersion = '1.6.6'
def lombokVersion = '1.18.30'

dependencies {
    // These dependencies are exported to consumers
    api "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api "org.slf4j:slf4j-api:${slf4jVersion}"

    // These dependencies are used internally, and not exposed to consumers on their own compile classpath.
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    testImplementation 'org.assertj:assertj-core:3.25.1'
    testImplementation "org.mockito:mockito-core:2.28.2"
}

test {
    useJUnitPlatform()
}

//------------------------------------------------------------------------------
// Publish to Maven Central
//------------------------------------------------------------------------------

signing {
    required { gradle.taskGraph.hasTask(publish) }
    sign publishing.publications
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Kibosh'
                description = 'The Kibosh project provides simple static analysis for Java source files designed to be run as part of a test suite.'
                url = 'https://github.com/dx42/Kibosh'

                scm {
                    connection = 'scm:git:git@github.com:dx42/Kibosh.git'
                    developerConnection = 'scm:git:git@github.com:dx42/Kibosh.git'
                    url = 'scm:git:git@github.com:dx42/Kibosh.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'chrismair'
                        name = 'Chris Mair'
                        email = 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
